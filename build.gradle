group 'io.ebean'
version '11.9.2'

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    maven {
      url "${artifactory_url}/plugins-release"
      credentials {
        username = "${artifactory_username}"
        password = "${artifactory_password}"
      }
    }
    maven {
      url "${artifactory_url}/libs-release-local"
      credentials {
        username = "${artifactory_username}"
        password = "${artifactory_password}"
      }
    }

  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.9"
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"

  }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'


if (project.hasProperty("signing.keyId")) {
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
}


repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

dependencies {
    compile group: 'io.ebean', name: 'ebean-agent', version: '11.9.1'

    compile gradleApi()
    compile localGroovy()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

jar {
    manifest.attributes provider: 'gradle'
}

pluginBundle {
  website = 'https://ebean-orm.github.io/docs/tooling/gradle'
  vcsUrl = 'https://github.com/ebean-orm-tools/ebean-gradle-plugin'
  description = 'Ebean ORM'
  tags = ['Ebean', 'ORM', 'JPA']
  plugins {
    ebeanPlugin {
      id = 'io.ebean'
      displayName = 'Ebean plugin'
    }
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
        name 'ebean-gradle-plugin'
        packaging 'jar'
        description 'Gradle plugin for enhancement Ebean entity beans'
        url 'http://github.com/ebean-orm-tools/ebean-gradle-plugin'

        scm {
          url 'scm:git@github.com/ebean-orm-tools/ebean-gradle-plugin.git'
          connection 'scm:git@github.com/ebean-orm-tools/ebean-gradle-plugin.git'
          developerConnection 'scm:git:https://github.com/ebean-orm-tools/ebean-gradle-plugin.git'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'rbygrave'
            name 'Robin Bygrave'
          }
          developer {
            id 'vnnv'
            name 'Victor Nikiforov'
          }
        }
      }
    }
  }
  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }

  artifactory {
    // disable gradle daemon before publish. doesn't work
    contextUrl = "${artifactory_url}"
    publish {
      repository {
        repoKey = 'libs-release-local' // The Artifactory repository key to publish to
        username = "${artifactory_username}" // The publisher user name
        password = "${artifactory_password}" // The publisher password
      }
      defaults {
        // Reference to Gradle publications defined in the build script.
        // This is how we tell the Artifactory Plugin which artifacts should be
        // published to Artifactory...

        publications('mavenJava')
        publishArtifacts = true
        // Properties to be attached to the published artifacts.
        properties = ['qa.level': 'basic', 'dev.team': 'core']
        // Publish generated POM files to Artifactory (true by default)
        publishPom = true
      }
    }
    resolve {
      repoKey = 'jcenter'
    }
  }
}
